include "collision data.txt"

macro get_tile_info(x,y) {
lda {y}
sep #$20
lda.b {x}+1
asl #2
rep #$20
tax
lda {collision_map},x
}

macro ceiling_collision(x,bits) {
txa
sep #$20
lda.b {x}+1
asl #2
rep #$20
tax
lda {collision_map},x
and {bits}
beq +
tsb {collision_flags}
lda {copy_y}
ora #$00ff
inc
sta {copy_y}
+;
}

macro ceiling_slope_collision(inc,bits) {
sta {temp_x}					//first it detects slopes going vertically
get_tile_info({temp_x},{copy_y})		//ceiling slopes are detected only when going from
and #$0002					//under the ceiling slope, to above ceiling slope
beq +
lda {temp_x}
and #$00f0
lsr #3
adc {slope_map},x
tax
lda {copy_y}
and #$ff00
clc
adc $0020,x
cmp {copy_y}
bcc +
dec
cmp {prev_y}
bcs +
adc #$0002
sta {copy_y}
lda #$0002
tsb {collision_flags}
+;
lda {temp_x}
cmp {center_x}
beq ++
-;
clc					//then detects slopes going horizontally
adc #{inc}				//it loops through every pixel when going horizontally
sta {temp_x}				//until it hits a slope height of the same whole pixel y value
get_tile_info({temp_x},{copy_y})
bit #{bits}
beq +
lda {temp_x}
and #$00f0
lsr #3
adc {slope_map},x
tax
lda {copy_y}
and #$ff00
clc
adc $0020,x
cmp {copy_y}
bcc +
tay
lda {copy_y}
and #$fff0
clc
adc #$0010
sta {prev_y}
dey
cpy {prev_y}
bcs +
iny #2
sty {copy_y}
lda #$0002
tsb {collision_flags}
+;
lda {temp_x}
cmp {center_x}
bne -
+;
}

macro floor_collision(x,bits) {
txa
sep #$20
lda.b {x}+1
asl #2
rep #$20
tax
lda {collision_map},x
and {bits}
beq +
tsb {collision_flags}
lda {bottom_y}
and #$ff00
dec
sta {bottom_y}
+;
}

macro floor_slope_collision(inc,bits) {
sta {temp_x}					//first it detects slopes going vertically
get_tile_info({temp_x},{bottom_y})		//slopes are detected only when going from
and #$0001					//over a slope, to under a slope
beq +
lda {temp_x}
and #$00f0
lsr #3
adc {slope_map},x
tax
lda {bottom_y}
and #$ff00
clc
adc $0000,x
cmp {bottom_y}
bcs +
cmp {prev_bottom_y}
bcc +
dec
sta {bottom_y}
lda #$0001
tsb {collision_flags}
+;
lda {temp_x}
cmp {center_x}
beq ++
-;
clc					//now detect slopes horizontally
adc #{inc}				//same rules apply as ceiling slopes
sta {temp_x}
get_tile_info({temp_x},{bottom_y})
bit #{bits}
beq +
lda {temp_x}
and #$00f0
lsr #3
adc {slope_map},x
tax
lda {bottom_y}
and #$ff00
clc
adc $0000,x
cmp {bottom_y}
bcs +
tay
lda {bottom_y}
and #$fff0
sec
sbc #$0010
sta {prev_bottom_y}
cpy {prev_bottom_y}
bcc +
dey
sty {bottom_y}
lda #$0001
tsb {collision_flags}
+;
lda {temp_x}
cmp {center_x}
bne -
+;
}

macro calculate_wall_detector_height() {
get_tile_info({center_x},{bottom_y})
and #$0001					//if on a slope or above a slope within a slope tile
beq +						//move bottom-wall detector up by half width amount
lda {center_x}					//to avoid colliding with walls while walking up slopes
and #$00f0
lsr #3
adc {slope_map},x
tax
lda {bottom_y}
and #$ff00
clc
adc $0000,x
cmp {bottom_y}
bcc +
lda {height}
sbc {half_width}
bra ++
+;
lda {height}
+;
clc
adc {copy_y}
sta {temp}
get_tile_info({center_x},{copy_y})		//now do the same for the top-wall detector
and #$0002
beq +
lda {center_x}
and #$00f0
lsr #3
adc {slope_map},x
tax
lda {copy_y}
and #$ff00
clc
adc $0020,x
cmp {copy_y}
bcs +
lda {copy_y}
adc {half_width}
sta {temp2}
bra ++
+;
lda {copy_y}
sta {temp2}
+;
}

macro right_wall_collision() {
sep #$20
lda.b {right_x}+1
asl #2
rep #$20
tax
lda {collision_map},x
and #$0004
beq +
tsb {collision_flags}
lda {right_x}
and #$ff00
dec
sta {right_x}
+;
}

macro left_wall_collision() {
sep #$20
lda.b {copy_x}+1
asl #2
rep #$20
tax
lda {collision_map},x
and #$0008
beq +
tsb {collision_flags}
lda {copy_x}
ora #$00ff
inc
sta {copy_x}
+;
}

//////////////////////////////////////////////////////////////////////

tile_collision:
lda {y_pos},x
sta {copy_y}
lda {x_pos},x
sta {copy_x}
lda {collision},x
bit #$0031
beq +
lda {collision_type}
cmp #$0001
bne +
lda {copy_y}		//gravity pull down when walking down slope
clc
adc #$0080
sta {copy_y}
+;
lda {collision},x
bit #$00c2
beq +
lda {collision_type}
cmp #$0002
bne +
lda {copy_y}		//gravity pull up when walking upside down
sec
sbc #$0080
sta {copy_y}
+;
stx {temp3}
stz {collision_flags}

/////////////////////////////////////////////////

vertical_collision:				//first check vertical collision
lda {width}
lsr
sta {half_width}
clc
adc {prev_x}
sta {prev_center_x}
clc
adc {half_width}
sta {prev_right_x}
lda {prev_y}
cmp {copy_y}
bcc down_collision				//if going down, jump to down collision routine
eor {copy_y}
and #$ff00					//only check if tile boundaries cross
beq no_ceiling_collision
get_tile_info({prev_center_x},{copy_y})		//if center is on or below a ceiling slope within a ceiling slope tile
and #$0002					//skip ceiling ledge detection
bne no_ceiling_collision
ceiling_collision({prev_x},#$0040)		//ceiling collision left
ceiling_collision({prev_right_x},#$0080)	//ceiling collision right
no_ceiling_collision:
lda {copy_y}
clc
adc {height}
sta {bottom_y}
lda {prev_y}
clc
adc {height}
sta {prev_bottom_y}
jmp horizontal_collision

down_collision:
adc {height}					//same rules as in ceiling color, but with floor collision
sta {prev_bottom_y}
lda {copy_y}
clc
adc {height}
sta {bottom_y}
eor {prev_bottom_y}
and #$ff00
beq no_floor_collision
get_tile_info({prev_center_x},{bottom_y})
and #$0001
bne no_floor_collision
floor_collision({prev_x},#$0010)
floor_collision({prev_right_x},#$0020)
lda {bottom_y}
sec
sbc {height}
sta {copy_y}
+;
no_floor_collision:

////////////////////////////////

horizontal_collision:
lda {copy_x}				//now do horizontal collision
clc
adc {width}
sta {right_x}
lda {copy_x}
clc
adc {half_width}
and #$fff0
sta {center_x}
lda {prev_x}
clc
adc {half_width}
and #$fff0
sta {temp}
cmp {center_x}
bcc right_collision
jmp left_collision			//if going left, jump to left collision routine
right_collision:
ceiling_slope_collision($0010,$0200)	//do ceiling slope collision
lda {copy_y}
clc
adc {height}
sta {bottom_y}
lda {temp}
floor_slope_collision($0010,$0200)	//do floor slope collision
lda {bottom_y}
sec
sbc {height}
sta {copy_y}
lda {right_x}
eor {prev_right_x}
and #$ff00
bne +
jmp exit_collision			//exit collision if right side of sprite does not cross tile boundaries
+;
calculate_wall_detector_height()	//this macro adjusts the top and bottom of the wall detectors
lda {temp2}				//to avoid getting stuck by walls when moving up slopes
right_wall_collision()
lda {temp}
right_wall_collision()
lda {right_x}
sec
sbc {width}
sta {copy_x}
exit_collision:
ldx {temp3}
lda {collision_flags}
sta {collision},x
beq +					//if no collision, skip saving coordinates
lda {copy_x}
sta {x_pos},x
lda {copy_y}
sta {y_pos},x
+;
rts

//////////////////////////////////////////////

left_collision:
ceiling_slope_collision($fff0,$0100)	//same rules apply as right collision
lda {copy_y}
clc
adc {height}
sta {bottom_y}
lda {temp}
floor_slope_collision($fff0,$0100)
lda {bottom_y}
sec
sbc {height}
sta {copy_y}
lda {copy_x}
eor {prev_x}
and #$ff00
bne +
jmp exit_collision
+;
calculate_wall_detector_height()
lda {temp2}
left_wall_collision()
lda {temp}
left_wall_collision()
jmp exit_collision